REM /**
REM  * RestfulJIRAWebHookServlet.bbj
REM  * @author stevett
REM  * written October 2015
REM  *
REM  * NOTE: There are some hard-wired things here that I don't like, but we don't have a full environment
REM  *       on nmcm, which is where this will run.  Could perhaps make more flexible later with
REM  *       CONFIG_VARS access, etc.
REM  * 
REM  */


use java.net.URLDecoder
use java.util.HashMap
use org.json.JSONObject
use org.json.JSONArray


class public RestfulJIRAWebHookServlet

    field private static BBjNumber TRUE = 1
    field private static BBjNumber FALSE = 0

    field private static BBjNumber RUNNING_ON_WINDOWS_DEV = #TRUE

    field private static BBjNumber SEND_RESPONSE_TEXT = #TRUE
    field private static BBjNumber SEND_EMAIL_DATA = #TRUE
    field private static BBjNumber INCLUDE_RAW_BODY_OF_REQUEST = #TRUE
    field private static BBjNumber UPDATE_SRQ = #TRUE

    field private BBjNumber chan
    field private BBjHttpSession session!
    field private BBjHttpRequest request!

    field private BBjVector parmNames!
    field private java.lang.String decodedBody!
    field private java.util.HashMap jsonDataMap!
    


    method public void handleRequestEvent(BBjServletEvent p_event!)

        declare BBjHttpResponse response!
        declare BBjHttpRequest request!

        #chan = unt
        #request! = cast(BBjHttpRequest, p_event!.getHttpRequest())
        #session! = cast(BBjHttpSession, #request!.getSession())
        
        response! = p_event!.getHttpResponse()
        response!.setContentType("text/html")
        
        open(#chan)"JSERVLET"
        #processRequest(#request!.getMethod(), #request!.getPathInfo())
        close(#chan) 

    methodend
    
    
        rem  This method is a test case 
    
    method public void getTestModeResponse()
    
      #chan = unt
      
      rem  Send output to a file, so you can see results:
      
      open(#chan, mode="O_CREATE") "/tmp/RestfulJIRAWebHookServlet-output.txt"
      #processRequest("GET", "/srq-status-change/ERP-1524")
      close(#chan)
      
    methodend
    
    
    method private void sendEmail(java.lang.String recipients!, java.lang.String subject!, java.lang.String body!)

        recipients$ = recipients!
        subject$ = subject!
        body$ = body!

        if #RUNNING_ON_WINDOWS_DEV then
            call "MailUtilities.bbj::send_simple_email_msg", recipients$, subject$, body$, mailReturnCode
        else
            bodyFileName$ = "/tmp/RestfulJIRAWebHookServlet.email.body." + str(tim) + ".txt"

            erase bodyFileName$,ERR=*next
            string bodyFileName$
            bodyFileChannel = unt
            open(bodyFileChannel) bodyFileName$
            print(bodyFileChannel) body$
            close(bodyFileChannel)

            emailCmd$ = "cat " + bodyFileName$ + " | mail -s """ + subject$ + """ -S smtp=""nmcz.notions-marketing.com"" " + recipients$
            returnCode = scall(emailCmd$)
        endif

    methodend
    
    
    method private void processRequest(java.lang.String httpMethod!, java.lang.String urlPath!)
        
        if urlPath!.startsWith("/srq-status-change/")  then
            
            if httpMethod!.equals("POST") then
            
                #decodedBody! = java.net.URLDecoder.decode(#request!.getBody(), "UTF-8")
                #loadJsonDataMap()
                
                srqNumber$ = #getSrqNumber()
                
                if len(srqNumber$) > 0 then

                    updateSrqLog$ = ""
                    
                    if #UPDATE_SRQ then
                        updateSrqLog$ = #updateSRQ(srqNumber$)
                    endif
                    
                    if #SEND_EMAIL_DATA or #SEND_RESPONSE_TEXT then
                    
                        requestData$ = #getRequestData()
                        
                        generalInfo$ = "<p>httpMethod! = " + httpMethod! + "</p>"
                        generalInfo$ = generalInfo$ + "<p>urlPath! = " + urlPath! + "</p>"
                        generalInfo$ = generalInfo$ + "<p>srqNumber$ = " + srqNumber$ + "</p>"
                        
                        requestData$ = generalInfo$ + requestData$

                        requestData$ = updateSrqLog$ + requestData$ 
        
                        if #SEND_EMAIL_DATA then
        
                            recipients$ = "steve.swett@notionsmarketing.com"
                            subject$ = "E-mail data for RestfulJIRAWebHookServlet"
                            body$ = requestData$
                            #sendEmail(recipients$, subject$, body$)
                            
                        endif
                        
                        if #SEND_RESPONSE_TEXT then
                        
                            if mailReturnCode > 0 then
                                print(#chan) "<p>E-mail failed with return code: " + str(mailReturnCode) + "</p>"
                            endif
                            
                            print(#chan) requestData$
                            
                        endif
                        
                    endif
                    
                
                endif
                
            endif
            
            if httpMethod!.equals("GET") then
                #getSampleResponseForGetMethod(urlPath!)
            endif
            
        endif

    methodend
    
    
    method private BBjString getSrqNumber()
    
        declare java.lang.String issueSummary!
        
        resultNum$ = ""
        
        if !(#jsonDataMap!.containsKey("error")) then
            
            issueSummary! = cast(java.lang.String, #jsonDataMap!.get("issue_summary"))
            foundAt = issueSummary!.indexOf("SRQ:0")

            if foundAt >= 0 then
            
                afterColon$ = issueSummary!.substring(foundAt + 4)
                
                if len(afterColon$) > 5 then
                    srqNum$ = afterColon$(1,6)
                    srqNum = num(srqNum$, ERR=bad_srq_num)
                    resultNum$ = srqNum$
                    bad_srq_num:
                endif
                
            endif
             
        endif
        
        methodret resultNum$
    
    methodend
    
    
    method private void loadJsonDataMap()
    
        declare org.json.JSONObject rootObject!
        declare org.json.JSONObject transitionObject!
        declare org.json.JSONObject userObject!
        declare org.json.JSONObject issueObject!
        declare org.json.JSONObject issueFieldsObject!
        
        #jsonDataMap! = new java.util.HashMap()
        
        seterr json_error_handler
        
        rootObject! = new org.json.JSONObject(#decodedBody!)
        
        REM  The following isn't need for data reading; it's about testing the structure of the JSON data
        transitionObject! = rootObject!.getJSONObject("transition")
        userObject! = rootObject!.getJSONObject("user")
        
        REM  These are needed for data reads:
        issueObject! = rootObject!.getJSONObject("issue")
        issueKey$ = issueObject!.getString("key")
        issueFieldsObject! = issueObject!.getJSONObject("fields")
        issueSummary$ = issueFieldsObject!.getString("summary")
         
        #jsonDataMap!.put("issue_key", issueKey$)
        #jsonDataMap!.put("issue_summary", issueSummary$)
        
        goto skip_json_error
        
        
        json_error_handler:
        
        #jsonDataMap!.put("error", errmes(-1))
        
        
        skip_json_error:
        
        seterr 0
        
    
    methodend
    
    
    method private java.lang.String getRequestData()
    
        requestData$ = ""
    
        requestData$ = requestData$ + "<p>Test of getting a few important pieces of data<p>"
        requestData$ = requestData$ + "<ul>"
        
        iter! = #jsonDataMap!.keySet().iterator()
        
        while iter!.hasNext()
        
            fieldKey! = iter!.next()
            requestData$ = requestData$ + "<li>" + fieldKey! + " = " + #jsonDataMap!.get(fieldKey!) 
          
        wend
        
        requestData$ = requestData$ + "</ul>"
        
    
        #parmNames! = #request!.getParameterNames()
        
        requestData$ = requestData$ + "<p>Complete list of parameter names shown in list below<p>"
        requestData$ = requestData$ + "<ul>"
        
        for x = 0 to #parmNames!.size() - 1
            requestData$ = requestData$ + "<li>" + #parmNames!.get(x) + "</li>"
        next x
        
        requestData$ = requestData$ + "</ul>"

        
        if #INCLUDE_RAW_BODY_OF_REQUEST then
            requestData$ = requestData$ + "<p>Raw body of request</p>"
            requestData$ = requestData$ + "<p><xmp>" + #decodedBody! + "</xmp></p>"
        endif
        
        
        methodret requestData$

    methodend
    
    
    method private java.lang.String updateSRQ(BBjString srqNumber!)
    
        declare java.lang.String issueKey!
        
        updateLog$ = ""
        
        REM  Initially just do in the local file system
        REM  TODO: Need to adapt to NMCM environment (data server)
        
        sysr_all_fields_iol:iolist K0$,K1$,HS$,HS1$,HS2$,REQ$,LOC$,STAT$,NOTES$,WHEN$,CONTACT$,ATTACHMENT$,ASSIGN$,ASTAMP$,CLSTAMP$,WORKED,PRI$
        
        srqNumber$ = srqNumber!
        srqKey$ = srqNumber$ + "00"
        
        sysrChannel = unt
        updateLog$ = updateLog$ + " before open SYSR file."
        open(sysrChannel) "SYSR"
        updateLog$ = updateLog$ + " after open SYSR file."
        
        read(sysrChannel, knum=0, key=srqKey$, dom=no_rec_found) IOL=sysr_all_fields_iol
        updateLog$ = updateLog$ + " read SYSR file."
        
        yymmddhhmm$ = date(0:"%Yz%Mz%Dz%Hz%mz") 
        
        STAT$ = "C"
        CLSTAMP$ = yymmddhhmm$
        
        write(sysrChannel) IOL=sysr_all_fields_iol
        updateLog$ = updateLog$ + " wrote SYSR file."
        
        trimmedRequester$ = cvs(REQ$,11)
        
        if len(trimmedRequester$) >= 0 then
            
            issueKey! = cast(java.lang.String, #jsonDataMap!.get("issue_key"))
            updateLog$ = updateLog$ + " got issueKey!."

            recipients$ = trimmedRequester$ + "@notionsmarketing.com"
            subject$ = "SRQ " + srqNumber$ + " has been closed.  (aka JIRA " + issueKey! + ")"
            body$ = "Original SRQ Description: " + cvs(NOTES$,2)
            
            updateLog$ = updateLog$ + " before sendEmail."
            #sendEmail(recipients$, subject$, body$)
            updateLog$ = updateLog$ + " after sendEmail."
            
        endif
        
        no_rec_found:
        
        close(sysrChannel)      
        updateLog$ = updateLog$ + " after close SYSR file."
        
        methodret updateLog$  
    
    methodend
    
    
    method private void getSampleResponseForGetMethod(java.lang.String urlPath!)

        print(#chan) "<p>This is the response given if method is GET.<p>"
        
        decodedText$ = java.net.URLDecoder.decode("Hello%20there", "UTF-8")
        
        print(#chan) "<p>This is the decodedText$ = " + decodedText$ + "</p>"
        

    methodend
    
    
classend
