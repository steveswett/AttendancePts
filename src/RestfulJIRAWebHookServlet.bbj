REM /**
REM  * RestfulJIRAWebHookServlet.bbj
REM  * @author stevett
REM  * written October 2015
REM  *
REM  * NOTE: There are some hard-wired things here that I don't like, but we don't have a full environment
REM  *       on nmcm, which is where this will run.  Could perhaps make more flexible later with
REM  *       CONFIG_VARS access, etc.
REM  * 
REM  */


use java.net.URLDecoder


class public RestfulJIRAWebHookServlet

    field private BBjNumber TRUE = 1
    field private BBjNumber FALSE = 0

    field private BBjNumber sendResponseText = #FALSE
    field private BBjNumber sendEmailData = #TRUE
    field private BBjNumber includeRawBodyOfRequest = #TRUE

    field private BBjNumber runningOnWindowsDev = #TRUE

    field private BBjNumber chan
    field private BBjHttpSession session!
    field private BBjHttpRequest request!

    field private BBjVector parmNames!


    method public void handleRequestEvent(BBjServletEvent p_event!)

        declare BBjHttpResponse response!
        declare BBjHttpRequest request!

        #chan = unt
        #request! = cast(BBjHttpRequest, p_event!.getHttpRequest())
        #session! = cast(BBjHttpSession, #request!.getSession())
        
        response! = p_event!.getHttpResponse()
        
        REM response!.setContentType("application/json")
        
        REM  The following is absolutely needed if I'm just going to return a bit of text to append
        REM  to some DOM node
        
        response!.setContentType("text/html")
        
        open(#chan)"JSERVLET"
        #processRequest(#request!.getMethod(), #request!.getPathInfo())
        close(#chan) 

    methodend
    
    
        rem  This method is a test case 
    
    method public void getTestModeResponse()
    
      #chan = unt
      
      rem  Send output to a file, so you can see results:
      
      open(#chan, mode="O_CREATE") "/tmp/RestfulJIRAWebHookServlet-output.txt"
      #processRequest("GET", "/srq-status-change/ERP-1524")
      close(#chan)
      
    methodend
    
    
    method public void processRequest(java.lang.String httpMethod!, java.lang.String urlPath!)
        
        if urlPath!.startsWith("/srq-status-change/")  then
            
            if httpMethod!.equals("POST") then
            
                requestData$ = #getRequestData()

                if #sendEmailData then
                    recipients$ = "steve.swett@notionsmarketing.com"
                    subject$ = "E-mail data for RestfulJIRAWebHookServlet"
                    
                    body$ = "<p>httpMethod! = " + httpMethod! + "</p>"
                    body$ = body$ + "<p>urlPath! = " + urlPath! + "</p>"
                    body$ = body$ + "<p>requestData = " + "</p>"
                    body$ = body$ + requestData$
                    
                    if #runningOnWindowsDev then
                        call "MailUtilities.bbj::send_simple_email_msg", recipients$, subject$, body$
                    else
                        emailCmd$ = "echo """ + body$ + """|mail -s """ + subject$ + """ -S smtp=""nmcz.notions-marketing.com"" " + recipients$
                        returnCode = scall(emailCmd$)
                    endif
                    
                endif
                
                if #sendResponseText then
                    print(#chan) requestData$
                endif
                
            endif
            
            if httpMethod!.equals("GET") then
                #getSampleResponseForGetMethod(urlPath!)
            endif
            
        endif

    methodend
    
    
    method public java.lang.String getRequestData()
    
        requestData$ = ""
    
        requestData$ = requestData$ + "<p>Test of getting a few important pieces of data<p>"
        requestData$ = requestData$ + "<ul>"
        
        requestData$ = requestData$ + "<li>webhookEvent = " + #request!.getParameter("webhookEvent") + "</li>"
        requestData$ = requestData$ + "<li>issue[key] = " + #request!.getParameter("issue[key]") + "</li>"
        requestData$ = requestData$ + "<li>issue[fields][summary] = " + #request!.getParameter("issue[fields][summary]") + "</li>"
        requestData$ = requestData$ + "<li>issue[fields][description] = " + #request!.getParameter("issue[fields][description]") + "</li>"
        requestData$ = requestData$ + "<li>user[emailAddress] = " + #request!.getParameter("user[emailAddress]") + "</li>"
        
        requestData$ = requestData$ + "</ul>"
        
    
        #parmNames! = #request!.getParameterNames()
        
        requestData$ = requestData$ + "<p>Complete list of parameter names shown in list below<p>"
        requestData$ = requestData$ + "<ul>"
        
        for x = 0 to #parmNames!.size() - 1
            requestData$ = requestData$ + "<li>" + #parmNames!.get(x) + "</li>"
        next x
        
        requestData$ = requestData$ + "</ul>"

        
        if #includeRawBodyOfRequest then
            requestData$ = requestData$ + "<p>Raw body of request</p>"
            decodedBody$ = java.net.URLDecoder.decode(#request!.getBody(), "UTF-8")
            requestData$ = requestData$ + "<p><xmp>" + decodedBody$ + "</xmp></p>"
        endif
        
        
        methodret requestData$

    methodend
    
    
    method public void getSampleResponseForGetMethod(java.lang.String urlPath!)

        print(#chan) "<p>This is the response given if method is GET.<p>"
        
        decodedText$ = java.net.URLDecoder.decode("Hello%20there", "UTF-8")
        
        print(#chan) "<p>This is the decodedText$ = " + decodedText$ + "</p>"
        

    methodend
    
    
classend
