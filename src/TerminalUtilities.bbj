REM /**
REM  * TerminalUtilities.bbj
REM  * @author steve
REM  *
REM  * Written: May 2015
REM  * 
REM  * Description: Miscellaneous terminal utilities.
REM  * 
REM  */


rem *- - - - - - - - - - CONSTANTS - - - - - - - - - -*

rem  See subroutine setup_constants


rem * - - - - - - - - - - FUNCTIONS - - - - - - - - - -*

functions:


def fnGetCurrentTime$(aDoIt)

    return date(0:"%hz:%mz:%sz%p")
    
fnend


def fnGetLoginUser$(aDoIt)

    tmpUser$ = info(3, 2)
    if len(tmpUser$) > 10 then tmpUser$ = tmpUser$(1,10)
    return tmpUser$
    
fnend


def fnGetUnqualifiedProgramName$(aProgramName$)

    programNameSlashTrim$ = fnTrimProgramName$(aProgramName$, "/") 
    programNameBackSlashTrim$ = fnTrimProgramName$(aProgramName$, "\")
    
    if len(programNameSlashTrim$) < len(programNameBackSlashTrim$) then
:       return programNameSlashTrim$

    return programNameBackSlashTrim$

fnend

     
     rem Get program name following last aFileSeparator$ character
     
def fnTrimProgramName$(aProgramName$, aFileSeparator$)

    program$ = aProgramName$
    
    separatorCount = pos(aFileSeparator$ = program$, 1, 0)
    
    if separatorCount > 0 then
:       lastSeparatorPos = pos(aFileSeparator$ = program$, 1, separatorCount);
:       program$ = program$(lastSeparatorPos + 1)

    return program$

fnend


rem * - - - - - - - - - - ENTRY POINTS - - - - - - - - - -*

entry_points:


get_current_time:

    enter formattedTime$
    
    gosub setup_constants
    
    formattedTime$ = fnGetCurrentTime$(TRUE)
    
    exit


get_unqualified_program_name:

    enter programNameIn$, unqualifiedProgramNameOut$
    
    gosub setup_constants
    
    unqualifiedProgramNameOut$ = fnGetUnqualifiedProgramName$(programNameIn$)
    
    exit


get_login_user:

    enter loginUser$
    
    gosub setup_constants
    
    loginUser$ = fnGetLoginUser$(TRUE)
    
    exit


display_screen_headings:

    enter programNameIn$, screenTitleIn$
    
    gosub setup_constants
    
    trimmedProgramName$ = fnGetUnqualifiedProgramName$(programNameIn$)
    termIdAndType$ = fid(0)
    lcOS$ = cvs(info(0,0), 8)

    if DISPLAY_DEVICE_NAME and pos("win" = lcOS$) = 0 then
        call "TERM90", device$, term$
        
        if len(device$) > 0 then
            termIdAndType$ = termIdAndType$ + "=" + device$
        endif 
    endif
    
    print @(0,0), pad(day, 20, "L", " ")
    print @(20,0), pad("NOTIONS MARKETING", 50, "L", " ")
    print @(50,0), pad(trimmedProgramName$, 30, "R", " ")
    
    print 'BU'
    print @(0,1), pad( fnGetCurrentTime$(TRUE) , 20, "L", " ")
    print @(20,1), pad(screenTitleIn$, 35, "L", " ")
    print @(55,1), pad( termIdAndType$ , 25, "R", " ")
    print 'EU' 

    exit


rem * - - - - - - - - - - SUBROUTINES - - - - - - - - - -*

subroutines:


setup_constants:

    TRUE = 1
    FALSE = 0
    
    DISPLAY_DEVICE_NAME = FALSE

    return
