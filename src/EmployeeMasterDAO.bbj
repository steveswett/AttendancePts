REM /**
REM  * EmployeeMasterDAO.bbj
REM  * @author stevett
REM  *
REM  * Written: August 2015
REM  * 
REM  * Description: Data Access Object for table EMPLOY
REM  *
REM  */


use java.util.LinkedHashMap
use java.util.HashMap

declare java.util.LinkedHashMap employeeRecordsMap!
declare java.util.HashMap employeeDataMap!


rem *- - - - - - - - - - CONSTANTS - - - - - - - - - -*

rem  This section never runs for multi-entry point called programs, so subroutine "setup_constants" is called
rem  for each entry point.


rem *- - - - - - - - - - FUNCTIONS - - - - - - - - - -*

functions:



rem *- - - - - - - - - - ENTRY POINTS - - - - - - - - - -*

entry_points:


get_name_last_first_mi:

    enter badge5In$, nameOut$
    
    gosub setup_constants
    
    gosub query_for_last_first_mi
    
    exit


set_attend_pts_date_to_today:

    enter badge5In$
    
    gosub setup_constants
    
    gosub update_pts_date_to_today
    
    exit


append_empno_to_map_of_maps:

    enter employeeRecordsMap!
    
    gosub setup_constants
    
    gosub insert_empno_into_map_of_maps
    
    exit


rem *- - - - - - - - - - SUBROUTINES - - - - - - - - - -*

subroutines:


REM  ------------------------------------------------------------------------------------------
REM  Get name like this: "LAST, FIRST M."
REM  ------------------------------------------------------------------------------------------


query_for_last_first_mi:

    employChannel = sqlunt
    sqlopen(employChannel) attendPtsDataSource$

    sqlprep(employChannel) GET_LAST_FIRST_MI_SELECT_STMT$

    nameOut$ = ""
    
    sqlexec(employChannel) badge5In$
    dim resultRow$:sqltmpl(employChannel)
    
    resultRow$ = sqlfetch(employChannel,err=emp_name_not_found)
    nameOut$ = cvs(resultRow.last_first_mi$, 2)
    
    emp_name_not_found:
    
    sqlclose(employChannel)

    return


REM  ------------------------------------------------------------------------------------------
REM  Set the points date in field ATNDT to the current date
REM  ------------------------------------------------------------------------------------------

    
update_pts_date_to_today:

    employChannel = sqlunt
    sqlopen(employChannel) attendPtsDataSource$

    sqlprep(employChannel) SET_ATTEND_PTS_DATE_TO_TODAY_STMT$
    sqlexec(employChannel) day, badge5In$
    
    sqlclose(employChannel)

    return


REM  ------------------------------------------------------------------------------------------
REM  Given a LinkedHashMap employeeRecordsMap!, which contains several inner maps (one per
REM  employee), add the "empno" into each inner map.
REM  ------------------------------------------------------------------------------------------
    
    
insert_empno_into_map_of_maps:

    employChannel = sqlunt
    sqlopen(employChannel) attendPtsDataSource$

    sqlprep(employChannel) GET_EMPNO_STMT$
    
    iter! = employeeRecordsMap!.keySet().iterator()
    
    while iter!.hasNext()
    
        badgeKey! = iter!.next()
        employeeDataMap! = cast( java.util.HashMap, employeeRecordsMap!.get(badgeKey!) )

        sqlexec(employChannel) badgeKey!
        
        dim resultRow$:sqltmpl(employChannel)
        
        empNo$ = "0"
        resultRow$ = sqlfetch(employChannel,err=emp_not_found)
        empNo$ = cvs(resultRow.empno$, 2)
        
        emp_not_found:
        
        employeeDataMap!.put("empno", empNo$)

    wend
    
    sqlclose(employChannel)

    return


REM  ------------------------------------------------------------------------------------------
REM  setup_constants
REM  ------------------------------------------------------------------------------------------

setup_constants:

    TRUE = 1
    FALSE = 0

    REM  TODO: In future, use Java static class to store config var names (to remove redundancy)
    
    call "ConfigVarsDAO.bbj::get_value", "new.attend.pts.files.data.source.name", configVarFound, configVarDataType$, attendPtsDataSource$
    
    GET_LAST_FIRST_MI_SELECT_STMT$ = "select rtrim(substring(name, 14, 14)) + ', ' + rtrim(substring(name, 1, 11)) + ' ' + rtrim(substring(name, 12, 2)) as last_first_mi " +
:                                    "from employ where badg = ?"
    
    SET_ATTEND_PTS_DATE_TO_TODAY_STMT$ = "update employ set atndt = ? where badg = ?"
    
    GET_EMPNO_STMT$ = "select empno from employ where badg = ?"
    
    return
    