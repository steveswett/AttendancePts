REM /**
REM  * BasisLicenseUsageDAO.bbj
REM  * @author stevett
REM  *
REM  * Written: December 5, 2015
REM  * 
REM  * Description: Data Access Object for table BasisLicenseUsage
REM  *
REM  */

use java.util.HashMap
use java.util.LinkedHashMap

declare java.util.HashMap recordsMapIn!
declare java.util.HashMap oneRecordMap!

declare java.util.LinkedHashMap countsMapOut!


rem *- - - - - - - - - - CONSTANTS - - - - - - - - - -*

rem  This section never runs for multi-entry point called programs, so subroutine "setup_constants" is called
rem  for each entry point.


rem *- - - - - - - - - - FUNCTIONS - - - - - - - - - -*

functions:



rem *- - - - - - - - - - ENTRY POINTS - - - - - - - - - -*

entry_points:


add_license_records:

    enter timestampIn$, recordsMapIn!

    gosub setup_constants
    
    gosub insert_license_records
    
    exit
    
    
get_counts_for_date_all_users:

    enter yyyymmddIn$, countsMapOut!
    
    gosub setup_constants
    
    gosub fetch_counts_for_date_all_users
    
    exit


rem *- - - - - - - - - - SUBROUTINES - - - - - - - - - -*

subroutines:


REM  ------------------------------------------------------------------------------------------
REM  Add license usage rows for all records in recordsMapIn!
REM  ------------------------------------------------------------------------------------------


insert_license_records:

    call "DateUtilities.bbj::get_timestamp_as_datetime_string", timestampIn$, sqlTimeStamp$

    licensesChannel = sqlunt
    sqlopen(licensesChannel) dataWarehouseDataSource$

    sqlprep(licensesChannel) INSERT_LICENSES_STMT$
    
    iter! = recordsMapIn!.keySet().iterator()
    
    while iter!.hasNext()
    
        recordNoKey! = iter!.next()
        oneRecordMap! = cast( java.util.HashMap, recordsMapIn!.get(recordNoKey!) )

        user$ = oneRecordMap!.get("user")
        server$ = oneRecordMap!.get("server")
        device$ = oneRecordMap!.get("device")
        version$ = oneRecordMap!.get("version")
        portInfo$ = oneRecordMap!.get("port_info")
        startInfo$ = oneRecordMap!.get("start_info")
        groupedCount$ = oneRecordMap!.get("grouped_count")

        sqlexec(licensesChannel) sqlTimeStamp$, recordNoKey!, user$, server$, device$, version$, portInfo$, startInfo$, groupedCount$            
    
    wend
    
    sqlclose(licensesChannel)

    return
    
    
REM  ------------------------------------------------------------------------------------------
REM  Fetch counts and load into countsMapOut!
REM  ------------------------------------------------------------------------------------------

fetch_counts_for_date_all_users:

    call "DateUtilities.bbj::get_yyyymmdd_with_hyphen_separators", yyyymmddIn$, sqlDate$
    
    usageChannel = sqlunt
    sqlopen(usageChannel) dataWarehouseDataSource$

    sqlprep(usageChannel) SELECT_COUNTS_FOR_ALL_USERS_STMT$

    countsMapOut!.clear()
    
    fromSqlDate$ = sqlDate$ + " 00:00:00"  
    toSqlDate$ = sqlDate$ + " 23:59:59"  

    sqlexec(usageChannel) fromSqlDate$, toSqlDate$ 
    dim resultRow$:sqltmpl(usageChannel)
    
    while TRUE
    
        resultRow$ = sqlfetch(usageChannel,err=*break)
        countsMapOut!.put(resultRow.CollectionTime$, resultRow.LicensesUsed )
        
    wend
    
    sqlclose(usageChannel)

    return


REM  ------------------------------------------------------------------------------------------
REM  setup_constants
REM  ------------------------------------------------------------------------------------------

setup_constants:

    TRUE = 1
    FALSE = 0

    dataWarehouseDataSource$ = ""
    call "ConfigVarsDAO.bbj::get_value", "notions.data.warehouse.data.source.name", configVarFound, configVarDataType$, dataWarehouseDataSource$

    
    INSERT_LICENSES_STMT$ = "insert into BasisLicenseUsage (CollectionTime, EntryNo, UserId, Server, Device, Version, PortInfo, StartInfo, GroupedCount) " +
:                        "values (?, ?, ?, ?, ?, ?, ?, ?, ?)"


    SELECT_COUNTS_FOR_ALL_USERS_STMT$ = "select " + 
:       "CollectionTime, " + 
:       "sum(BasisLicenseUsage.GroupedCount) as LicensesUsed " +
:       "from dbo.BasisLicenseUsage " +
:       "where BasisLicenseUsage.CollectionTime between ? and ? " +
:       "group by BasisLicenseUsage.CollectionTime " +
:       "order by BasisLicenseUsage.CollectionTime"
    
    return
