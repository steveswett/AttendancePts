REM /**
REM  * ConfigVarsDAO.bbj
REM  * @author stevett
REM  * 
REM  * Written: July 2015
REM  * 
REM  * Description: Data Access Object for table CONFIG_VARS
REM  *
REM  */


REM  Performance improvements needed.  This does an SQL open, prepare, execute, close each time a config var is requested.
REM  Currently under time constraints, so not done now (7/22/15).  THIS COULD BE DONE WITH BASIC READS/WRITES, IF DESIRED.


rem *- - - - - - - - - - CONSTANTS - - - - - - - - - -*

rem  This section never runs for multi-entry point called programs, so subroutine "setup_constants" is called
rem  for each entry point.


rem *- - - - - - - - - - FUNCTIONS - - - - - - - - - -*

functions:



rem *- - - - - - - - - - ENTRY POINTS - - - - - - - - - -*

entry_points:

get_value:

    REM  In the future, may need a way to return values as different data types

    enter configVarKeyIn$, recordFoundOut, configVarValueTypeOut$, configVarValueAsStringOut$
    
    gosub setup_constants
    gosub open_sql_channels
    gosub prepare_sql_stmts
    gosub get_config_var_value
    gosub close_sql_channels
    
    exit


rem *- - - - - - - - - - SUBROUTINES - - - - - - - - - -*

subroutines:


REM  ------------------------------
REM  setup_constants
REM  ------------------------------

setup_constants:

    TRUE = 1
    FALSE = 0
    
    REM  The Data Source Name for the Config Var info is stored in the VZ file record with the "CV" key
    VZ_KEY_CONFIG_VAR_DATA_SOURCE$ = "CV"

    REM  Default value:
    configVarsDataSource$ = "DftConfigVarsDB"
    
    seterr abort_vz_read

    call "OPEN90", vzChannel, "VZ", noTemplate$
    read(vzChannel, key=VZ_KEY_CONFIG_VAR_DATA_SOURCE$, err=vz_read_fail) dataSourceName$
    configVarsDataSource$ = cvs(dataSourceName$, 2)
    vz_read_fail:
    close(vzChannel)
    
    abort_vz_read:
    
    GET_CONFIG_VAR_SELECT_STMT$ = "select cv_value, value_type from config_vars where cv_key = ?"
    
    return


REM  ------------------------------
REM  open_sql_channels
REM  ------------------------------

open_sql_channels:

    getConfigVarChannel = sqlunt
    sqlopen(getConfigVarChannel) configVarsDataSource$
    
    return


REM  ------------------------------
REM  prepare_sql_stmts
REM  ------------------------------

prepare_sql_stmts:

    sqlprep(getConfigVarChannel) GET_CONFIG_VAR_SELECT_STMT$

    return


REM  ------------------------------
REM  get_config_var_value
REM  ------------------------------

get_config_var_value:

    sqlexec(getConfigVarChannel) configVarKeyIn$
    dim configVarResultRow$:sqltmpl(getConfigVarChannel)
    configVarResultRow$ = sqlfetch(getConfigVarChannel,err=row_not_found)
    goto row_found
    
    row_not_found:
    
    recordFoundOut = FALSE
    configVarValueTypeOut$ = ""
    configVarValueAsStringOut$ = ""
    goto end_get_config_var
    
    row_found:
    
    recordFoundOut = TRUE
    configVarValueTypeOut$ = configVarResultRow.value_type$
    configVarValueAsStringOut$ = cvs(configVarResultRow.cv_value$, 2)
    
    if configVarResultRow.value_type$ = "B" then
        
        configVarValUC$ = cvs(configVarValueAsStringOut$, 4)
        
        if configVarValUC$ = "TRUE" then configVarValueAsStringOut$ = "1" 
        if configVarValUC$ = "FALSE" then configVarValueAsStringOut$ = "0" 
    
    endif
     
    goto end_get_config_var
    
    end_get_config_var:
    
    return


REM  ------------------------------
REM  close_sql_channels
REM  ------------------------------

close_sql_channels:

    sqlclose(getConfigVarChannel)

    return    
    
