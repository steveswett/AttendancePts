REM /**
REM  * ProcessIOStatsDAO.bbj
REM  * @author stevett
REM  *
REM  * Written: December 11, 2015
REM  * 
REM  * Description: Data Access Object for table ProcessIOStats
REM  *
REM  */

use java.util.HashMap

declare java.util.HashMap recordsMapIn!
declare java.util.HashMap oneRecordMap!


rem *- - - - - - - - - - CONSTANTS - - - - - - - - - -*

rem  This section never runs for multi-entry point called programs, so subroutine "setup_constants" is called
rem  for each entry point.


rem *- - - - - - - - - - FUNCTIONS - - - - - - - - - -*

functions:


rem  If the incoming number is bad, return a "0"

def fnGetNumericOrZero$(aNumberIn$)

    aNumberOut = num(aNumberIn$,err=number_error)
    aNumberOut$ = aNumberIn$
    goto number_ok
    
    number_error:
    print "bad number (too big?) - aNumberIn$ = ", aNumberIn$
    aNumberOut$ = "0"
        
    number_ok:

    return aNumberOut$

fnend



rem *- - - - - - - - - - ENTRY POINTS - - - - - - - - - -*

entry_points:


add_stat_records:

    enter timestampIn$, recordsMapIn!

    call "DateUtilities.bbj::get_timestamp_as_datetime_string", timestampIn$, sqlTimeStamp$
    
    gosub setup_constants
    
    gosub insert_stat_records
    
    exit


rem *- - - - - - - - - - SUBROUTINES - - - - - - - - - -*

subroutines:


REM  ------------------------------------------------------------------------------------------
REM  Add stat rows for all records in recordsMapIn!
REM  ------------------------------------------------------------------------------------------


insert_stat_records:

    cmdsChannel = sqlunt
    sqlopen(cmdsChannel) dataWarehouseDataSource$
    sqlprep(cmdsChannel) GET_CMDS_STMT$
    
    statsChannel = sqlunt
    sqlopen(statsChannel) dataWarehouseDataSource$
    sqlprep(statsChannel) INSERT_STATS_STMT$
    
    iter! = recordsMapIn!.keySet().iterator()
    
    while iter!.hasNext()
    
        pidKey! = iter!.next()
        oneRecordMap! = cast( java.util.HashMap, recordsMapIn!.get(pidKey!) )
        
        cmd$ = oneRecordMap!.get("cmd")

        REM  Get overridden command(s) from ProcessStatistics table:
        
            REM  Defaults:
            statsCmd$ = cmd$
            statsOrigCmd$ = ""
            
            sqlexec(cmdsChannel) serverName$, pidKey!, sqlTimeStamp$
            dim cmdsResultRow$:sqltmpl(cmdsChannel)
            
            cmdsResultRow$ = sqlfetch(cmdsChannel,err=cmds_record_not_found)
            
            statsCmd$ = cvs(cmdsResultRow.Command$, 2)
            statsOrigCmd$ = cvs(cmdsResultRow.OrigCommand$, 2)
            
            cmds_record_not_found:

        priority$ = oneRecordMap!.get("priority")
        user$ = oneRecordMap!.get("user")
        
        diskRead$ = oneRecordMap!.get("disk_read")
        diskWrite$ = oneRecordMap!.get("disk_write")
        swapIn$ = oneRecordMap!.get("swap_in")
        io$ = oneRecordMap!.get("io")
        
        rem  Validate/default the numbers:
        
        diskRead$ = fnGetNumericOrZero$(diskRead$)
        diskWrite$ = fnGetNumericOrZero$(diskWrite$)
        swapIn$ = fnGetNumericOrZero$(swapIn$)
        io$ = fnGetNumericOrZero$(io$)

        sqlexec(statsChannel) serverName$, pidKey!, sqlTimeStamp$, statsCmd$, statsOrigCmd$, user$, priority$, diskRead$, diskWrite$, swapIn$, io$     
    
    wend
    
    sqlclose(cmdsChannel)
    sqlclose(statsChannel)

    return


REM  ------------------------------------------------------------------------------------------
REM  setup_constants
REM  ------------------------------------------------------------------------------------------

setup_constants:

    TRUE = 1
    FALSE = 0
    
    serverName$ = serverenv("HOSTNAME")

    dataWarehouseDataSource$ = ""
    call "ConfigVarsDAO.bbj::get_value", "erp.performance.data.source.name", configVarFound, configVarDataType$, dataWarehouseDataSource$

    GET_CMDS_STMT$ = "select Command, OrigCommand from ProcessStatistics where ServerName = ? and ProcessId = ? and CollectionTime = ?"
    
    INSERT_STATS_STMT$ = "insert into ProcessIOStats (ServerName, ProcessId, CollectionTime, Command, OrigCommand, UserId, Priority, DiskReadKBperSec, DiskWriteKBperSec, SwapInPercent, IOPercent) " +
:                        "values (?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?)"
    
    return
