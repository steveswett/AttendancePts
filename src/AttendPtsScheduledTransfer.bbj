REM /**
REM  * AttendPtsScheduledTransfer.bbj
REM  * @author stevett
REM  * 
REM  * Written: July 2015
REM  * 
REM  * Description: Scheduled job used to transfer data from WFMR to table ATTEND_PTS_WORK
REM  *
REM  */


use java.util.LinkedHashMap
use java.util.HashMap

declare java.util.LinkedHashMap recordsMap!
declare java.util.HashMap dataMap!


begin


rem *- - - - - - - - - - CONSTANTS - - - - - - - - - -*


rem *- - - - - - - - - - FUNCTIONS - - - - - - - - - -*

functions:


def fnRunSQL(pStmt$)

    rtnVal = 0

    sqlprep(wfmrDataChannel) pStmt$
    sqlexec(wfmrDataChannel)
    
    dim resultRow$:sqltmpl(wfmrDataChannel)
    resultRow$ = sqlfetch(wfmrDataChannel,err=row_not_found)
    
    fromDate$ = resultRow.from_date$
    toDate$ = resultRow.to_date$ 
    rtnVal = 1
    
    row_not_found:
    
    return rtnVal
    
fnend


rem *- - - - - - - - - - MAIN ROUTINE - - - - - - - - - -*

main_routine:


    print "Started at ", date(0:"%Y/%Mz/%Dz %hz:%mz:%sz%p")

    gosub init
    
    gosub mass_load

    print "Finished at ", date(0:"%Y/%Mz/%Dz %hz:%mz:%sz%p")

    eoj:
    release

end


rem *- - - - - - - - - - SUB-ROUTINES - - - - - - - - - -*


REM  ------------------------------------------------------------
REM  init
REM  ------------------------------------------------------------

init:

    weekday$ = date(0: "%Ds")
    
    daysToGoBack = 2
    
    if weekday$ = "Sat" then daysToGoBack = daysToGoBack + 1 
    if weekday$ = "Sun" then daysToGoBack = daysToGoBack + 2 
    
    
    REM TODO: We may need to go back 1 additional day for each holiday encountered between "today" and "today - daysToGoBack".
    REM       However, we don't have an easy way to get holidays:
    REM          Blue Screen: "HO" key in VZ file.  But data isn't accurate.  For example, 7/3/15 is missing
    REM          WFMR: Lab_Time_Event table.  But "occurrence definitions" are used instead of fixed dates.  Not all fields understood.
    

    preferredDateRangeSelectStmt$ = 
:       "select top 1 " +
:       "convert(varchar(10), start_date, 120) as from_date, " +
:       "convert(varchar(10), DATEADD(day, -"+
:       str(daysToGoBack) +
:       " ,getdate()), 120) as to_date " +
:       "from lab_pay_period " +
:       "where start_date < DATEADD(day, -"+
:       str(daysToGoBack) +
:       " ,getdate()) " +
:       "order by start_date desc"


    REM  Fallback position is to use a range "current date - 7" through "current date - 2"
    
    daysToGoBackAndFiveMore = daysToGoBack + 5

    fallbackDateRangeSelectStmt$ = 
:       "select " + 
:       "convert(varchar(10), DATEADD(day, -"+
:       str(daysToGoBackAndFiveMore) +
:       " ,getdate()), 120) as from_date, " +
:       "convert(varchar(10), DATEADD(day, -"+
:       str(daysToGoBack) +
:       " ,getdate()), 120) as to_date" 


    wfmrDataSource$ = ""
    call "ConfigVarsDAO.bbj::get_value", "wfmr.data.source.name", configVarFound, configVarDataType$, wfmrDataSource$

    return


REM  ------------------------------------------------------------
REM  mass_load
REM  ------------------------------------------------------------

mass_load:

    wfmrDataChannel = sqlunt
    sqlopen(wfmrDataChannel) wfmrDataSource$

    usingPreferred = fnRunSQL(preferredDateRangeSelectStmt$)
    
    if !(usingPreferred) then success = fnRunSQL(fallbackDateRangeSelectStmt$) 

    sqlclose(wfmrDataChannel)

    print "Using preferred date range? ", iff(usingPreferred, "yes", "no")
    print "daysToGoBack = ", daysToGoBack 
    print "daysToGoBackAndFiveMore = ", daysToGoBackAndFiveMore 
    print "fromDate$ = ", fromDate$
    print "toDate$ = ", toDate$
    print " date range select stmt = "
    print $0A$, iff(usingPreferred, preferredDateRangeSelectStmt$, fallbackDateRangeSelectStmt$)
    
    fromYYMD$ = fromDate$(1,4) + fromDate$(6,2) + fromDate$(9,2) 
    toYYMD$ = toDate$(1,4) + toDate$(6,2) + toDate$(9,2)
    
    fromYYMD = num(fromYYMD$)
    toYYMD = num(toYYMD$)
    
    print $0A$, "Count status BEFORE:"
    gosub print_count_status
     
    call "AttendPtsWorkDAO.bbj::mass_load_wfmr_data", fromDate$, toDate$
    
    print $0A$, "Count status AFTER:"
    gosub print_count_status

    return
    
    
REM  ------------------------------------------------------------
REM  print count status
REM  ------------------------------------------------------------
    
print_count_status:

    recordsMap! = new java.util.LinkedHashMap()
    call "AttendPtsWorkDAO.bbj::get_count_by_exc_date_and_source_map", fromYYMD, toYYMD, recordsMap!
    
    iter! = recordsMap!.keySet().iterator()
    
    while iter!.hasNext()
    
        idKey! = iter!.next()
    
        dataMap! = cast( java.util.HashMap, recordsMap!.get(idKey!) )
          
        wfmrExcDate$ = dataMap!.get("wfmr_exc_date")
        wfmrSrc$ = dataMap!.get("wfmr_src")
        qty = dataMap!.get("qty")
        
        print "date src qty = ", wfmrExcDate$, " ", wfmrSrc$, " ", qty  
      
    wend
    
    print ""

    return
