REM /**
REM  * AttendPtsScheduledTransfer.bbj
REM  * @author stevett
REM  *
REM  */


begin


rem *- - - - - - - - - - CONSTANTS - - - - - - - - - -*


rem *- - - - - - - - - - FUNCTIONS - - - - - - - - - -*


def fnRunSQL(pStmt$)

    rtnVal = 0

    sqlprep(wfmrDataChannel) pStmt$
    sqlexec(wfmrDataChannel)
    
    dim resultRow$:sqltmpl(wfmrDataChannel)
    resultRow$ = sqlfetch(wfmrDataChannel,err=row_not_found)
    
    fromDate$ = resultRow.from_date$
    toDate$ = resultRow.to_date$ 
    rtnVal = 1
    
    row_not_found:
    
    return rtnVal
    
fnend


rem *- - - - - - - - - - MAIN ROUTINE - - - - - - - - - -*

main_routine:


    gosub init
    
    gosub mass_load

    eoj:
    release

end


rem *- - - - - - - - - - SUB-ROUTINES - - - - - - - - - -*


REM  ------------------------------------------------------------
REM  init
REM  ------------------------------------------------------------

init:

    weekday$ = date(0: "%Ds")
    
    daysToGoBack = 2
    
    if weekday$ = "Sat" then daysToGoBack = daysToGoBack + 1 
    if weekday$ = "Sun" then daysToGoBack = daysToGoBack + 2 
    
    
    REM TODO: We may need to go back 1 additional day for each holiday encountered between "today" and "today - daysToGoBack".
    REM       However, we don't have an easy way to get holidays:
    REM          Blue Screen: "HO" key in VZ file.  But data isn't accurate.  For example, 7/3/15 is missing
    REM          WFMR: Lab_Time_Event table.  But "occurrence definitions" are used instead of fixed dates.  Not all fields understood.
    

    preferredDateRangeSelectStmt$ = 
:       "select top 1 " +
:       "convert(varchar(10), start_date, 120) as from_date, " +
:       "convert(varchar(10), DATEADD(day, -"+
:       str(daysToGoBack) +
:       " ,getdate()), 120) as to_date " +
:       "from lab_pay_period " +
:       "where start_date < DATEADD(day, -"+
:       str(daysToGoBack) +
:       " ,getdate()) " +
:       "order by start_date desc"


    REM  Fallback position is to use a range "current date - 7" through "current date - 2"
    
    daysToGoBackAndFiveMore = daysToGoBack - 5

    fallbackDateRangeSelectStmt$ = 
:       "select " + 
:       "convert(varchar(10), DATEADD(day, -"+
:       str(daysToGoBackAndFiveMore) +
:       " ,getdate()), 120) as from_date, " +
:       "convert(varchar(10), DATEADD(day, -"+
:       str(daysToGoBack) +
:       " ,getdate()), 120) as to_date" 


    wfmrDataSource$ = ""
    call "ConfigVarsDAO.bbj::get_value", "wfmr.data.source.name", configVarFound, configVarDataType$, wfmrDataSource$

    wfmrDataChannel = sqlunt
    sqlopen(wfmrDataChannel) wfmrDataSource$

    return


REM  ------------------------------------------------------------
REM  mass_load
REM  ------------------------------------------------------------

mass_load:

    success = fnRunSQL(preferredDateRangeSelectStmt$)
    
    if !(success) then success = fnRunSQL(fallbackDateRangeSelectStmt$) 

    sqlclose(wfmrDataChannel)

    REM  print "dates: from, to = ", fromDate$, ", ", toDate$
    
    call "AttendPtsWorkDAO.bbj::mass_load_wfmr_data", fromDate$, toDate$

    return

