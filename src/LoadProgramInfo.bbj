REM /**
REM  * LoadProgramInfo.bbj
REM  * @author stevett
REM  *
REM  */

use java.io.File
use org.apache.commons.collections.iterators.ArrayIterator
use java.util.HashSet

declare java.io.File directory!
declare java.io.File file!
declare org.apache.commons.collections.iterators.ArrayIterator arrayIterator!
declare java.util.HashSet filesAccessedHashSet!
declare java.util.HashSet programsCalledHashSet!


begin


rem *- - - - - - - - - - FUNCTIONS - - - - - - - - - -*

functions:

def fnGetDateWrittenOrRevised$(aSearchPhrase$, aStmt$, aUpperCaseStmt$)

    rem  print aStmt$

    result$ = ""
    foundPosition = pos(aSearchPhrase$ = aUpperCaseStmt$)
    
    if foundPosition > 0 and len(stmt$) > foundPosition + len(aSearchPhrase$) + 1 then
        remark$ = stmt$(foundPosition + len(aSearchPhrase$) + 1)
        
        REM  Find last slash position:b
        lastSlashPos = pos("/" = remark$, -1)
        
        if lastSlashPos > 0 and len(remark$) >= lastSlashPos + 2 then 
            
            if lastSlashPos >= 6 and remark$(lastSlashPos - 3, 1) = "/" then
                result$ = remark$(lastSlashPos - 5, 8)
            else
                if lastSlashPos >= 3 then
                    result$ = remark$(lastSlashPos - 2, 5)
                endif
            endif
            
        endif
        
    endif
    
    return result$

fnend


rem *- - - - - - - - - - MAIN ROUTINE - - - - - - - - - -*

main_routine:

    gosub setup_constants
    
    gosub init
    
    gosub read_source_programs


rem *- - - - - - - - - - EOJ - - - - - - - - - -*

eoj:

    REM  release
    end


rem *- - - - - - - - - - SUBROUTINES - - - - - - - - - -*


    rem ------------------------------------------------------------------------------------------
    rem  One-time intialization
    rem ------------------------------------------------------------------------------------------

    
init:

    call "ConfigVarsDAO.bbj::get_value", "program.info.data.source.name", configVarFound, configVarDataType$, programInfoDataSource$

    REM  Delete all program info (will reload)
    
    deleteChannel = sqlunt
    sqlopen(deleteChannel) programInfoDataSource$
    
    sqlprep(deleteChannel) DELETE_PROGRAMS_QUERY$
    sqlexec(deleteChannel)
    
    sqlprep(deleteChannel) DELETE_PROGRAM_FILES_QUERY$
    sqlexec(deleteChannel)
    
    sqlprep(deleteChannel) DELETE_PROGRAM_PROGRAMS_QUERY$
    sqlexec(deleteChannel)

    sqlclose(deleteChannel)
    
    return


    rem ------------------------------------------------------------------------------------------
    rem  Read through source programs in SOURCE_PROGRAM_PATH$
    rem ------------------------------------------------------------------------------------------
    
    
read_source_programs:

    directory! = new java.io.File(SOURCE_PROGRAM_PATH$)
    
    arrayIterator! = new org.apache.commons.collections.iterators.ArrayIterator(directory!.listFiles())
    
    programsChannel = sqlunt
    sqlopen(programsChannel) programInfoDataSource$
    sqlprep(programsChannel) INSERT_PROGRAMS_QUERY$
    
    programFilesChannel = sqlunt
    sqlopen(programFilesChannel) programInfoDataSource$
    sqlprep(programFilesChannel) INSERT_PROGRAM_FILES_QUERY$
    
    programProgramsChannel = sqlunt
    sqlopen(programProgramsChannel) programInfoDataSource$
    sqlprep(programProgramsChannel) INSERT_PROGRAM_PROGRAMS_QUERY$
    
    while arrayIterator!.hasNext()
    
        file! = cast( java.io.File, arrayIterator!.next())
        
        if !(file!.isFile()) then continue
        
        REM  If empty file, skip
        if file!.length() = 0 then continue
        
        sourceProgramName$ = file!.getName()
        
        REM if !(sourceProgramName$ = "FONE92") then continue
        
        gosub process_one_source_program

    wend
    
    sqlclose(programsChannel)
    sqlclose(programFilesChannel)
    sqlclose(programProgramsChannel)
 
    return
    

    rem ------------------------------------------------------------------------------------------
    rem  Process one source program -- sourceProgramName$
    rem ------------------------------------------------------------------------------------------


process_one_source_program:

    print sourceProgramName$
    
    sourceProgramChannel = unt
    open(sourceProgramChannel) SOURCE_PROGRAM_PATH$ + "/" + sourceProgramName$
    
    relativeLineNo = 0
    dateWrittenFound = FALSE
    dateRevisedFound = FALSE
    programDescription$ = ""
    dateWritten$ = ""
    dateRevised$ = ""
    
    filesAccessedHashSet! = new java.util.HashSet()
    programsCalledHashSet! = new java.util.HashSet()
    
    while TRUE
    
        read(sourceProgramChannel, err=*break) rawStmt$
        REM print rawStmt$
        
        REM  Handle multi-line statements:
        
        try_split:
        
        semiColonPos = pos(";" = rawStmt$)
        
        if semiColonPos > 0 then
            stmt$ = rawStmt$(1, semiColonPos - 1)
            gosub process_one_statement
            
            if len(rawStmt$) > len(stmt$) + 2 then
                rawStmt$ = rawStmt$(len(stmt$) + 2)
                goto try_split
            endif
        else
            stmt$ = rawStmt$
            gosub process_one_statement
        endif
        
        
    
    wend
    
    programDescription$ = cvs(programDescription$,3)
    if len(programDescription$) > 250 then programDescription$ = programDescription$(1,250)
    
    sqlexec(programsChannel) sourceProgramName$, programDescription$, dateWritten$, dateRevised$  
    
REM     print "files accessed list ""channel - file"""

    iter! = filesAccessedHashSet!.iterator()
    
    while iter!.hasNext()
        channelAndFile$ = iter!.next()
        colonsPos = pos("::" = channelAndFile$)
REM         print channelAndFile$(1, colonsPos - 1), " - ", channelAndFile$(colonsPos + 2)
         sqlexec(programFilesChannel) sourceProgramName$, channelAndFile$(colonsPos + 2), channelAndFile$(1, colonsPos - 1) 
    wend

    
REM     print "programs called list"

    iter! = programsCalledHashSet!.iterator()
    
    while iter!.hasNext()
        programCalled$ = iter!.next()
REM         print programCalled$
        sqlexec(programProgramsChannel) sourceProgramName$, programCalled$ 
    wend
    
    close(sourceProgramChannel)

    return
    

    rem ------------------------------------------------------------------------------------------
    rem  Process a single statement
    rem ------------------------------------------------------------------------------------------
    
    
process_one_statement:

    relativeLineNo = relativeLineNo + 1
    
    upperCaseStmt$ = cvs(stmt$, 4)
    
    remarkPosition = pos("REM" = upperCaseStmt$)

    REM  Determine date written:
    
    if relativeLineNo <= 10 and !(dateWrittenFound) then
        dateWritten$ = fnGetDateWrittenOrRevised$("REM ORG", stmt$, upperCaseStmt$)

        if len(dateWritten$) > 0 then
            dateWrittenFound = TRUE
        endif
        
    endif
    
    REM  Determine date revised:
    
    if relativeLineNo <= 10 and !(dateRevisedFound) then
        dateRevised$ = fnGetDateWrittenOrRevised$("REM REV", stmt$, upperCaseStmt$)

        if len(dateRevised$) > 0 then
            dateRevisedFound = TRUE
        endif
        
    endif

    
    REM  Determine program description:
    
    if relativeLineNo <= 5 and !(dateWrittenFound) and !(dateRevisedFound) then
        
        if remarkPosition > 0 and len(stmt$) > remarkPosition + 4 then
            remark$ = stmt$(remarkPosition + 4)
            programDescription$ = programDescription$ + remark$
        endif
        
    endif
    
    
    REM  Spot an open90ed file:
    open90Position = pos("""OPEN90""" = stmt$)
    
    if open90Position > 0 and (remarkPosition = 0 or remarkPosition > open90Position) then
        open90AndAfter$ = stmt$(open90Position)
        firstCommaPos = pos("," = open90AndAfter$,1,1)
        secondCommaPos = pos("," = open90AndAfter$,1,2)
        thirdCommaPos = pos("," = open90AndAfter$,1,3)
        
        if firstCommaPos > 0 and secondCommaPos > 0 and thirdCommaPos > 0 then
            channel$ = cvs(open90AndAfter$(firstCommaPos + 1, secondCommaPos - firstCommaPos - 1), 3) 
            accessedFile$ = cvs(open90AndAfter$(secondCommaPos + 1, thirdCommaPos - secondCommaPos - 1), 3)
            
            REM  Trim quotes off accessedFile$
            accessedFile$ = accessedFile$(2, len(accessedFile$) - 2)

            filesAccessedHashSet!.add(channel$ + "::" + accessedFile$)
        endif
        
    endif
    
    
    REM  Spot an opened file:
    openPosition = pos("OPEN(" = stmt$)
    if openPosition = 0 then openPosition = pos("OPEN (" = stmt$)
    
    if openPosition > 0 and (remarkPosition = 0 or remarkPosition > openPosition) then
        openAndAfter$ = stmt$(openPosition)
        leftParenPos = pos("(" = openAndAfter$)
        rightParenPos = pos(")" = openAndAfter$)
        
        if leftParenPos > 0 and rightParenPos > 0 and len(openAndAfter$) > rightParenPos then
            channel$ = cvs(openAndAfter$(leftParenPos + 1, rightParenPos - leftParenPos - 1), 3)
            
            REM  Trim off options after channel, if present
            commaPos = pos("," = channel$)
            
            if commaPos > 0 then
                channel$ = channel$(1, commaPos - 1)
            endif
             
            accessedFile$ = cvs(openAndAfter$(rightParenPos + 1), 3)
            
            REM  Trim quotes off accessedFile$
            if accessedFile$(1,1) = """" then
                accessedFile$ = accessedFile$(2, len(accessedFile$) - 2)
            endif

            filesAccessedHashSet!.add(channel$ + "::" + accessedFile$)
        endif
        
    endif
    
    
    REM  Spot a called program:
    callPosition = pos("XCALL " = stmt$)
    if callPosition = 0 then callPosition = pos("CALL " = stmt$)
    
    if callPosition > 0 and (remarkPosition = 0 or remarkPosition > callPosition) then
        callAndAfter$ = stmt$(callPosition)
        firstQuotePos = pos("""" = callAndAfter$,1,1)
        secondQuotePos = pos("""" = callAndAfter$,1,2)
        
        if firstQuotePos > 0 and secondQuotePos > 0 then
            callPhrase$ = cvs(callAndAfter$(firstQuotePos + 1, secondQuotePos - firstQuotePos - 1), 3) 
            
            REM  Trim off entry point, if present
            entryPtPos = pos("::" = callPhrase$)
            
            if entryPtPos > 0 then
                callPhrase$ = callPhrase$(1, entryPtPos - 1)
            endif

            programsCalledHashSet!.add(callPhrase$)
        endif
        
    endif

    return
    

    rem ------------------------------------------------------------------------------------------
    rem  Setup constants - makes last subroutine due to line continuations
    rem ------------------------------------------------------------------------------------------


setup_constants:

    TRUE = 1
    FALSE = 0
    
    SOURCE_PROGRAM_PATH$ = "/bbxListedPrograms"
    
    DELETE_PROGRAMS_QUERY$ = "delete from program"
    
    DELETE_PROGRAM_FILES_QUERY$ = "delete from program_files"
    
    DELETE_PROGRAM_PROGRAMS_QUERY$ = "delete from program_programs"
    
    INSERT_PROGRAMS_QUERY$ = "insert into program (program_name, description, date_written, last_revised) " +
:       "values(?,?,?,?)"

    INSERT_PROGRAM_FILES_QUERY$ = "insert into program_files (program_name, file_path, file_channel) " +
:       "values(?,?,?)"

    INSERT_PROGRAM_PROGRAMS_QUERY$ = "insert into program_programs (program_name, called_program) " +
:       "values(?,?)"

    return