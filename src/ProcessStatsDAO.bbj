REM /**
REM  * ProcessStatsDAO.bbj
REM  * @author stevett
REM  *
REM  * Written: December 2, 2015
REM  * 
REM  * Description: Data Access Object for table ProcessStatistics
REM  *
REM  */

use java.util.HashMap

declare java.util.HashMap recordsMapIn!
declare java.util.HashMap oneRecordMap!


rem *- - - - - - - - - - CONSTANTS - - - - - - - - - -*

rem  This section never runs for multi-entry point called programs, so subroutine "setup_constants" is called
rem  for each entry point.


rem *- - - - - - - - - - FUNCTIONS - - - - - - - - - -*

functions:



rem *- - - - - - - - - - ENTRY POINTS - - - - - - - - - -*

entry_points:


add_stat_records:

    enter timestampIn$, recordsMapIn!

    call "DateUtilities.bbj::get_timestamp_as_datetime_string", timestampIn$, sqlTimeStamp$
    
    gosub setup_constants
    
    gosub insert_stat_records
    
    exit


rem *- - - - - - - - - - SUBROUTINES - - - - - - - - - -*

subroutines:


REM  ------------------------------------------------------------------------------------------
REM  Add stat rows for all records in recordsMapIn!
REM  ------------------------------------------------------------------------------------------


insert_stat_records:

    statsChannel = sqlunt
    sqlopen(statsChannel) dataWarehouseDataSource$

    sqlprep(statsChannel) INSERT_STATS_STMT$
    
    iter! = recordsMapIn!.keySet().iterator()
    
    while iter!.hasNext()
    
        pidKey! = iter!.next()
        oneRecordMap! = cast( java.util.HashMap, recordsMapIn!.get(pidKey!) )
        
        cmd$ = oneRecordMap!.get("cmd")
        origCmd$ = oneRecordMap!.get("orig_cmd")
        user$ = oneRecordMap!.get("user")
        tty$ = oneRecordMap!.get("tty")
        state$ = oneRecordMap!.get("state")
        cpuPercent$ = oneRecordMap!.get("cpu_pct")
        elapsedTimeInSeconds = oneRecordMap!.get("elapsed_tm_secs")
        origElapsedTimeInSeconds = oneRecordMap!.get("orig_elapsed_tm_secs")
        cpuTimeInSeconds = oneRecordMap!.get("cpu_time_secs")
        cpuTimePerSecond = oneRecordMap!.get("cpu_time_per_sec")
        memoryData$ = oneRecordMap!.get("data_memory_kb")

        sqlexec(statsChannel) pidKey!, sqlTimeStamp$, cmd$, origCmd$, user$, tty$, state$, cpuPercent$, elapsedTimeInSeconds, origElapsedTimeInSeconds, cpuTimeInSeconds, cpuTimePerSecond, memoryData$    
    
    wend
    
    sqlclose(statsChannel)

    return


REM  ------------------------------------------------------------------------------------------
REM  setup_constants
REM  ------------------------------------------------------------------------------------------

setup_constants:

    TRUE = 1
    FALSE = 0

    dataWarehouseDataSource$ = ""
    call "ConfigVarsDAO.bbj::get_value", "notions.data.warehouse.data.source.name", configVarFound, configVarDataType$, dataWarehouseDataSource$
    
    INSERT_STATS_STMT$ = "insert into ProcessStatistics (ProcessId, CollectionTime, Command, OrigCommand, UserId, TtyDevice, State, CPUPercent, ElapsedTimeInSeconds, OrigElapsedTimeInSeconds, CPUTimeInSeconds, CPUTimePerSecond, DataMemoryKB) " +
:                        "values (?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?)"
    
    return
