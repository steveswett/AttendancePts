REM /**
REM  * ApproveAttPtsDispPopup.bbj
REM  * @author stevett
REM  *
REM  * Written: July 2015
REM  * 
REM  * Description: Disposition popup window for use in Supervisor Approval of Attendance Points.
REM  * 
REM  */


use java.util.LinkedHashMap

declare java.util.LinkedHashMap dispCodesMap!


rem *- - - - - - - - - - CONSTANTS - - - - - - - - - -*

rem  This section never runs for multi-entry point called programs, so subroutine "setup_constants" is called
rem  for each entry point.


rem *- - - - - - - - - - FUNCTIONS - - - - - - - - - -*


def fnSkipDispCode(aDispCode!)

    tmpSkip = FALSE
    
    if isFMLAMode then
        if aDispCode! <> "F" and aDispCode! <> "P" and aDispCode! <> "D" then tmpSkip = TRUE
    else
        if aDispCode! = "P" or aDispCode! = "D" then tmpSkip = TRUE
    endif
    
    return tmpSkip

fnend    



rem *- - - - - - - - - - ENTRY POINTS - - - - - - - - - -*

entry_points:
    
    
get_description:

    enter dispCodeIn$, descriptionOut$

    gosub setup_constants

    gosub init
    
    gosub get_descr
    
    exit


show_popup:

    enter isFMLAMode, upperLeftColumn, upperLeftRow, dispCode$
    
    gosub setup_constants

    print 'push'

    gosub init

    gosub display_popup
    
    gosub handle_input
    
    print 'pop'
    
    exit


rem *- - - - - - - - - - SUBROUTINES - - - - - - - - - -*

subroutines:


    rem ------------------------------------------------------------------------------------------
    rem  setup_constants
    rem ------------------------------------------------------------------------------------------

    
setup_constants:

    TRUE = 1
    FALSE = 0
    
    SHOW_FIELD_TERMINATOR_PRESSED = FALSE
    
    START_ROW_FOR_LIST = 1
    
    UP_ARROW_CUSTOM_INPUT_VALUE$ = $86$
    UP_ARROW_CTL_OUTPUT_VALUE$ = $1D$
    
    DOWN_ARROW_CUSTOM_INPUT_VALUE$ = $87$
    DOWN_ARROW_CTL_OUTPUT_VALUE$ = $1E$

    return


    rem ------------------------------------------------------------------------------------------
    rem  One-time intialization
    rem ------------------------------------------------------------------------------------------

    
init:

        rem For info on function key programming, see this article: http://www.basis.com/kb00025
        rem Also see Basis help: PRO/5 > User's Ref > Character Devices > Terminals > Function and Edit Keys
    
        rem Below is how 'EL' (edit keys) mnemonics are used to assign a key a value
        
        rem "2" - load only one key
        rem $02$ - means the up arrow key (see the documentation)
        rem chr(1) - means the custom value of the key is 1 byte long
        rem UP_ARROW_CUSTOM_INPUT_VALUE$ - is the custom INPUT value of the key
        
    print 'EL', "2", $02$, chr(1), UP_ARROW_CUSTOM_INPUT_VALUE$,
    print 'EL', "2", $03$, chr(1), DOWN_ARROW_CUSTOM_INPUT_VALUE$,
    
    upArrowPair$ = UP_ARROW_CUSTOM_INPUT_VALUE$ + UP_ARROW_CTL_OUTPUT_VALUE$
    downArrowPair$ = DOWN_ARROW_CUSTOM_INPUT_VALUE$ + DOWN_ARROW_CTL_OUTPUT_VALUE$
    
    resultIgnored$ = stbl("!TERMS", upArrowPair$ + downArrowPair$ + stbl("!TERMS"))
    
    dispCodesMap! = new java.util.LinkedHashMap()
    
    dispCodesMap!.put("N", "Not Handled")
    dispCodesMap!.put("A", "Approve WFMR Values")
    dispCodesMap!.put("E", "Edit")
    dispCodesMap!.put("F", "FMLA Pending")
    dispCodesMap!.put("P", "FMLA Approved")
    dispCodesMap!.put("D", "FMLA Denied")

    if isFMLAMode then
        endRowForList = 3
    else
        endRowForList = 4
    endif

    
    return


    rem ------------------------------------------------------------------------------------------
    rem  Get the description for a single code
    rem ------------------------------------------------------------------------------------------

get_descr:

    tmpDescr! = dispCodesMap!.get(dispCodeIn$)
    
    if tmpDescr! = null() then
        descriptionOut$ = ""
    else
        descriptionOut$ = tmpDescr!
    endif

    return


    rem ------------------------------------------------------------------------------------------
    rem  Display the popup window
    rem ------------------------------------------------------------------------------------------

    
display_popup:

    print 'window' (upperLeftColumn, upperLeftRow, 31, 8, "Choose Disposition",'br'),
    
    rem The row and column values used here are relative to inside the window
    
    print 'er',
    
    cursorRow = START_ROW_FOR_LIST
    x = 0
    
    iter! = dispCodesMap!.keySet().iterator()
    
    while iter!.hasNext()
    
      tmpDispCode! = iter!.next()
      
      if fnSkipDispCode(tmpDispCode!) then continue
      
      x = x + 1
      
      print @(5, x),  dispCodesMap!.get(tmpDispCode!)
      
      if dispCode$ = tmpDispCode! then cursorRow = x
      
    wend

    return


    rem ------------------------------------------------------------------------------------------
    rem  Handle input: Up Arrow, Down Arrow, Enter
    rem ------------------------------------------------------------------------------------------

    
handle_input:

    while TRUE
            
        input(len=1) @(2, cursorRow), ignoreInput$
        
        savedCTL = CTL
        
        isArrowing = FALSE
        needsBreak = FALSE
    
        switch CTL
        
            case ASC(UP_ARROW_CTL_OUTPUT_VALUE$)
                cursorRow = cursorRow - 1
                isArrowing = TRUE
            
                break

            case ASC(DOWN_ARROW_CTL_OUTPUT_VALUE$)
                cursorRow = cursorRow + 1
                isArrowing = TRUE
            
                break

            case 0
                needsBreak = TRUE
            
                break
             
        swend
        
        if needsBreak then break

        if cursorRow < START_ROW_FOR_LIST then
            cursorRow = START_ROW_FOR_LIST
        else 
            if cursorRow > endRowForList then cursorRow = endRowForList
        endif

        if isArrowing and SHOW_FIELD_TERMINATOR_PRESSED then
            print @(2, endRowForList + 1), 'CL', "CTL, savedCTL: ", CTL, " ", savedCTL
        endif
        
    wend
    
    iter! = dispCodesMap!.keySet().iterator()
    x = 0
    
    while iter!.hasNext()
    
      tmpDispCode! = iter!.next()
      
      if fnSkipDispCode(tmpDispCode!) then continue
      
      x = x + 1
      
      if x = cursorRow then
          dispCode$ = tmpDispCode!
          break
      endif
      
    wend
    
    
    return